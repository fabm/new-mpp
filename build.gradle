plugins {
    id 'kotlin-multiplatform' version '1.3.10'
    id 'org.jetbrains.kotlin.frontend' version '0.0.44'
}

repositories {
    jcenter()
    maven { url "http://dl.bintray.com/kotlin/ktor" }
    maven { url 'https://kotlin.bintray.com/kotlin-js-wrappers' }
    mavenCentral()
}
def ktor_version = '1.0.0'
def logback_version = '1.2.3'

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.js, 'js') {
            configure([compilations.main, compilations.test]) {
                tasks.getByName(compileKotlinTaskName).kotlinOptions {
                    sourceMapEmbedSources = "always"
                    sourceMap = true
                    moduleKind = "commonjs"
                    metaInfo = true
                }
            }

            configure(compilations.main) {
                tasks.getByName(compileKotlinTaskName).kotlinOptions {
                    main = "call"
                }
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jvmMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                implementation "io.ktor:ktor-server-jetty:$ktor_version"
                implementation "io.ktor:ktor-html-builder:$ktor_version"
                implementation "ch.qos.logback:logback-classic:$logback_version"
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
        jsMain {
            dependencies {
                implementation 'org.jetbrains:kotlin-react:16.6.0-pre.68-kotlin-1.3.20'
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
                implementation "org.jetbrains:kotlin-react-dom:16.6.0-pre.68-kotlin-1.3.20"
            }
        }
        jsTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }
    }
}

kotlinFrontend {
    npm {
        dependency "react"
        dependency "react-dom"
        devDependency("karma")
    }

    sourceMaps = true

    webpackBundle {
        bundleName = "main"
        proxyUrl = "http://localhost:8080"
    }
}

ktor {
    port = 8080
    mainClass = "io.ktor.server.jetty.EngineMain"
}